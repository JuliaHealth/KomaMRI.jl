{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Diffusion-induced Signal Attenuation"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using KomaMRI #hide\n",
    "using PlotlyJS #hide\n",
    "using Random, Suppressor #hide"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The purpose of this tutorial is to showcase the simulation of diffusion-related effects.\n",
    "For this, we are going to define a `path` motion to simulate the Brownian motion of spins.\n",
    "This is not the most efficient way of simulating diffusion, but it is a good way to understand the phenomenon.\n",
    "In particular, we will going to simulate isotropic diffusion, characterized by the Apparent Diffusion Coefficient (ADC)."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Creating a phantom with isotropic diffusion"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "First we will define a `Phantom` without motion containing $10,000$ spins. The spins will have the same\n",
    "relaxation times $T_1 = 1000\\,\\mathrm{ms}$ and $T_2 = 100\\,\\mathrm{ms}$, and will be placed at the origin."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "Nspins = 10_000\n",
    "obj = Phantom(;\n",
    "    x  = zeros(Nspins),\n",
    "    T1 = ones(Nspins) * 1000e-3,\n",
    "    T2 = ones(Nspins) * 100e-3,\n",
    ");"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now we will define the Brownian motion of the spins using the `path` motion definition.\n",
    "The motion will be defined by the displacements in the x, y, and z directions (`dx`, `dy`, and `dz`)\n",
    "of the spins. The displacements will be generated by a random walk with mean square displacement\n",
    "\n",
    "$$\n",
    "\\mathbb{E}\\left[x^{2}\\right]=2D Δt,\n",
    "$$\n",
    "where $D$ is the diffusion coefficient and $Δt$ is time step duration."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "D = 2e-9                # Diffusion Coefficient of water in m^2/s\n",
    "T = 100e-3              # Duration of the motion\n",
    "Nt = 100                # Number of time steps\n",
    "Δt = T / (Nt - 1)       # Time sep\n",
    "Δr = sqrt(2 * D * Δt);  # √ Mean square displacement"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Random walk is defined as the cumulative sum of random displacements:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "rng = MersenneTwister(1234) # Setting up the random seed\n",
    "dx = cumsum([zeros(Nspins) Δr .* randn(rng, Nspins, Nt - 1)]; dims=2)\n",
    "dy = cumsum([zeros(Nspins) Δr .* randn(rng, Nspins, Nt - 1)]; dims=2)\n",
    "dz = cumsum([zeros(Nspins) Δr .* randn(rng, Nspins, Nt - 1)]; dims=2);"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Including the `random_walk` into the `Phantom` definition:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "random_walk = KomaMRI.path(dx, dy, dz, TimeRange(0.0, T))\n",
    "obj.motion = random_walk\n",
    "p1 = plot_phantom_map(obj, :T1; time_samples=Nt÷4, height=450)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The plot shows the random walk of spins due to diffusion, also known as Brownian motion.\n",
    "This motion was named after Robert Brown, who first described the phenomenon in 1827 while\n",
    "looking at pollen suspended in water under a microscope."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Pulse Gradient Spin Echo (PGSE) sequence"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "The classical sequence used to measure diffusion is the pulse gradient spin echo (PGSE)\n",
    "introduced by Stejskal and Tanner in 1965. This sequence is characterized by the use of two diffusion\n",
    "gradients, placed right before and right after the inversion RF pulse. The duration of each gradient is\n",
    "defined by the δ parameter and the distance between the beginning of both gradients is described by the\n",
    "Δ parameter. In this tutorial square-shaped gradients will be used.\n",
    "\n",
    "First, we generate the RF pulses:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "sys   = Scanner()\n",
    "durRF = 1e-3\n",
    "B1    = (π / 2) / (2π * γ * durRF)\n",
    "rf90  = PulseDesigner.RF_hard(B1, durRF, sys)\n",
    "rf180 = (0.0 + 2im) * rf90;"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now we generate the gradients:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "G = 30e-3            # Gradient amplitude\n",
    "δ = 30e-3            # Duration of the gradient\n",
    "Δ = durRF + δ        # Time between the two gradients\n",
    "gx_diff = Grad(G, δ);"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Finally, we generate the ADC:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "adc_dwell_time = 1e-6\n",
    "adc = ADC(1, adc_dwell_time, durRF/2 - adc_dwell_time/2); # ADCs with N=1 are positioned at the center"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Obtaining the PGSE sequence:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "seq = Sequence()\n",
    "seq += rf90\n",
    "seq += gx_diff\n",
    "seq += rf180\n",
    "seq += gx_diff\n",
    "seq += adc\n",
    "p2 = plot_seq(seq; show_adc=true, height=300) # Plotting the sequence"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "For the isotropic diffusion, the signal attenuation is given by the Stejskal-Tanner formula:\n",
    "\n",
    "$$\n",
    "E = \\exp\\left(-b D\\right)\n",
    "$$\n",
    "\n",
    "where \\(b\\) is the b-value, defined as:\n",
    "\n",
    "$$\n",
    "b = (\\gamma G \\delta)^{2} \\cdot \\left(\\Delta - \\delta/3\\right)\n",
    "$$\n",
    "\n",
    "where $\\gamma$ is the gyromagnetic ratio, $G$ is the gradient amplitude, $\\delta$ is the gradient duration,\n",
    "and $\\Delta$ is the time between the two gradients."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function bvalue(seq)\n",
    "    block, axis = 2, 1 # Gx from second block\n",
    "    G = seq.GR[axis, block].A\n",
    "    δ = seq.GR[axis, block].T\n",
    "    Δ = dur(seq[2:3]) # Because there are no gaps\n",
    "    b = (2π * γ * G * δ)^2 * (Δ - δ/3)\n",
    "    return b * 1e-6\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Diffusion Weighted Imaging (DWI)\n",
    "For DWI, multiple b-values are acquired to determine the tissue's ADC.\n",
    "For this, we will scale the gradient's amplitude of the previous sequence to obtain a desired b-value.\n",
    "We will store the sequences in a vector `seqs` and simulate the signal for each one of them."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "seqs = Sequence[] # Vector of sequences\n",
    "bvals = [0, 250, 500, 1000, 1500, 2000] # b-values in s/mm^2\n",
    "for bval_target in bvals\n",
    "    gradient_scaling = sqrt(bval_target / bvalue(seq))\n",
    "    seq_b = gradient_scaling * seq\n",
    "    push!(seqs, seq_b)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "To simulate, we will broadcast the `simulate` function over the sequences and store the signals in a vector `Sb`.\n",
    "The `Ref`'s are used to avoid broadcasting the `obj` and `sys` arguments (they will remain constant for all `seqs`)."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "sim_params = KomaMRICore.default_sim_params()\n",
    "sim_params[\"return_type\"] = \"mat\"\n",
    "sim_params[\"Δt\"] = Δt; # Set max. grad. time step to fit diffusion time step\n",
    "\n",
    "signals = @suppress simulate.(Ref(obj), seqs, Ref(sys); sim_params); # simulate broadcasted over seqs\n",
    "\n",
    "Sb = [sb[1] for sb in signals] # Reshaping the simulated signals\n",
    "bvals_si = bvals .* 1e6; # Convert b-values from s/mm^2 to s/m^2\n",
    "\n",
    "E_simulated   = abs.(Sb) ./ abs.(Sb[1])\n",
    "E_theoretical = exp.(-bvals_si .* D);\n",
    "s_sim  = scatter(x=bvals, y=E_simulated,   name=\"Simulated\") #hide\n",
    "s_theo = scatter(x=bvals, y=E_theoretical, name=\"exp(-b D)\", line=attr(dash=\"dash\")) #hide\n",
    "layout = Layout(title=\"Diffusion-induced signal attenuation E(b)\", xaxis=attr(title=\"b-value [s/mm^2]\")) #hide\n",
    "p3 = plot([s_sim, s_theo], layout) #hide"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The plot shows the signal attenuation as a function of the b-value. The simulated signal attenuation\n",
    "matches the theoretical curve, showing the expected exponential decay with the b-value."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.5"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.5",
   "language": "julia"
  }
 },
 "nbformat": 4
}
